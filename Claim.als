/*
 * 請求書関係の階層構造のモデル検査
 * これは受注モデルとの関係も含む
 * 請求明細の内部のモデルは現時点では別と考えている
 */
module Claim

open Order

-- 請求明細は
sig 請求明細{
	対象: set 大項目
}{
	-- 請求明細の親(表紙)はひとつ
	one 請求内容.this
	-- 請求対象の大項目はひとつの受注の内容の部分集合(複数の受注の内容が混じることはありえない)
	one x: 受注 | 対象 in x.内容
}

sig 表紙{
	請求内容: set 請求明細
}{
	-- 表紙の親(鑑)はひとつ
	one 請求内容.this
	-- 表紙の下にある請求明細は同一プロジェクト(関連プロジェクト考慮)の範囲内
	one 請求内容.対象.細分化.担当.親.代表
}

sig 鑑{
	請求内容: set 表紙
}{
	-- 鑑の親(送付状)はひとつ
	one 同封.this
	-- 鑑の下にある請求は同一請求先の範囲内
	one (内容.(請求内容.請求内容.対象)).請求先
}

sig 送付状{
	同封: set 鑑
}{
	-- 送付状の下は送付先が同じものはすべて同梱可能
	one (内容.(同封.請求内容.請求内容.対象)).送付先
}

fact {
	-- ひとつの表紙の配下にある請求明細で大項目を重複することはない(ダイアモンド合流しない)
	no z: 大項目 | some x: 表紙 | some disj y, y': 請求明細 | (y in x.請求内容 and y' in x.請求内容) and (z in y.対象 and z in y'.対象)
	-- ひとつの鑑の配下にある請求明細で同項目を重複することはない
	no z: 大項目 | some x: 鑑 | some disj y, y': 表紙 | (y in x.請求内容 and y' in x.請求内容) and (z in y.請求内容.対象 and z in y'.請求内容.対象)
	-- ひとつの送付状の配下にある請求明細で同項目を重複することはない
	no z: 大項目 | some x: 送付状 | some disj y, y': 鑑 | (y in x.同封 and y' in x.同封) and (z in y.請求内容.請求内容.対象 and z in y'.請求内容.請求内容.対象)
}

assert 請求明細の親はひとつ{
	all x: 請求明細 | one 請求内容.x
}
check 請求明細の親はひとつ for 3 but 10 請求明細

assert 表紙の親はひとつ{
	all x: 表紙 | one 請求内容.x
}
check 表紙の親はひとつ for 3 but 10 表紙

assert 鑑の親はひとつ{
	all x: 鑑 | one 同封.x
}
check 鑑の親はひとつ for 3 but 10 鑑

assert 表紙の下にある請求明細は同一プロジェクトの範囲内{
	all x: 表紙 | one x.請求内容.対象.細分化.担当.親.代表
}
check 表紙の下にある請求明細は同一プロジェクトの範囲内 for 5 but 3 表紙

assert 鑑の下にある請求は同一請求先の範囲内{
	all x: 鑑 | one (内容.(x.請求内容.請求内容.対象)).請求先
}
check 鑑の下にある請求は同一請求先の範囲内 for 5 but 3 鑑

assert 送付状の下は送付先が同一のものを同梱可能{
	all x: 送付状 | one (内容.(x.同封.請求内容.請求内容.対象)).送付先
}
check 送付状の下は送付先が同一のものを同梱可能 for 5 but 3 送付状

--------------------------------------------------------------------
-- 絞り込み確認用の述語
--------------------------------------------------------------------
pred 小項目を含まない(x: 大項目){
	no x.細分化
}
pred 大項目を含まない(x: 請求明細){
	no x.対象
}
pred 複数の大項目を持つ(x: 請求明細){
	#x.対象 > 1
}
pred 請求明細を含まない(x: 表紙){
	no x.請求内容
}
pred 請求明細を含まない(x: 鑑){
	no x.請求内容
}
pred 複数の表紙を持つ(x: 鑑){
	#x.請求内容 > 1
}
pred 関連プロジェクトを持つ(x: 表紙){
	some y: プロジェクト8桁関連, y': プロジェクト8桁代表 | y in x.請求内容.対象.細分化.担当.親 and y' in x.請求内容.対象.細分化.担当.親
}

pred show {
	some x: 大項目 | 複数の小項目を持つ[x]
	no x: 受注 | 大項目を含まない[x]
	no x: 請求明細 | 大項目を含まない[x]
	no x: 大項目 | 小項目を含まない[x]
	some x: 請求明細 | 複数の大項目を持つ[x]
	no x: 表紙 | 請求明細を含まない[x]
	no x: 鑑 | 請求明細を含まない[x]
	some x: 鑑 | 複数の表紙を持つ[x]
	some x: 表紙 | 関連プロジェクトを持つ[x]
	使われてないプロジェクトは無視[]
	使われてない住所は無視[]
	使われてないクライアントは無視[]
}
run show for 6
